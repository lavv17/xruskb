dnl Process this file with autoconf to produce a configure script.
AC_INIT(xrus.c)
AM_CONFIG_HEADER(config.h)

AM_INIT_AUTOMAKE(xruskb, 1.15.1)

if test "$CFLAGS" = ""
then
   CFLAGS="-O"
fi

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_LN_S
AC_PROG_INSTALL
AC_PATH_PROGS(M4, m4, [$missing_dir/missing m4])

if test "$CC" = gcc
then
   CFLAGS="$CFLAGS -Wall -Wno-implicit"
fi

dnl Checks for header files.
AC_PATH_XTRA

if test x$have_x != xyes; then
   AC_MSG_ERROR(
[You have to have X11 development package (include files and libraries) to compile xruskb.])
fi

AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS(unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_PID_T
AC_CONST

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_CHECK_FUNCS(strerror atexit strdup)

dnl Xaw/Motif tests
AC_ARG_WITH(Xaw,     [  --with-Xaw              compile with Athena widget set])
AC_ARG_WITH(motif,   [  --with-motif            compile with Motif (or lesstif)])
AC_ARG_WITH(toolkit, [  --with-toolkit=TK       use specified toolkit (motif, xaw, none)])

case "$with_toolkit" in
motif)with_motif=yes
      AC_DEFINE(TK,TK_MOTIF)
      ;;
xaw)  with_Xaw=yes
      AC_DEFINE(TK,TK_XAW)
      ;;
none) with_Xaw=no
      with_motif=no
      AC_DEFINE(TK,TK_NONE)
      ;;
"")   ;; # no tk specified, use first available
*)    AC_MSG_ERROR([invalid toolkit name, use motif, xaw, or none])
      ;;
esac

AC_DEFUN(XRUS_CHECK_XAW,
[
   AC_CHECK_LIB($1,main,[
	TOOLKIT_LIBS="-l$1"
	AC_DEFINE(HAVE_LIBXAW)
	],[],[-lXt -lXmu -Xext -lX11 $X_PRE_LIBS $X_LIBS $X_EXTRA_LIBS])
])


# if --with-Xaw was specified, check libXaw first
if test x$with_Xaw = xyes
then
   XRUS_CHECK_XAW(Xaw3d)
   if test -z "TOOLKIT_LIBS"; then
      XRUS_CHECK_XAW(Xaw)
   fi
fi

# if we don't use Xaw directly, test for Motif
if test x$ac_cv_lib_Xaw_main != xyes -a x$ac_cv_lib_Xaw3d_main != xyes \
   || test x$with_Xaw != xyes
then

if test "$with_motif" != no; then
dnl Check for Motif
AC_PATH_MOTIF
if test x$no_motif != xyes
then
   AC_DEFINE(HAVE_MOTIF)
   if test "x$motif_includes" != x && test "x$motif_includes" != x/usr/include
   then
      TOOLKIT_INCLUDE="-I$motif_includes"
      AC_SUBST(TOOLKIT_INCLUDE)
   fi
   TOOLKIT_LIBS="-lXm"
   if test "x$motif_libraries" != x && test "x$motif_libraries" != x/usr/lib
   then
      case "$X_LIBS" in
      *-R*) TOOLKIT_LIBS="-R$motif_libraries $TOOLKIT_LIBS";;
      esac
      TOOLKIT_LIBS="-L$motif_libraries $TOOLKIT_LIBS"
   fi

   # motif can require additional libs
   AC_CHECK_LIB(gen,regex,[TOOLKIT_LIBS="$TOOLKIT_LIBS -lgen"])
   AC_CHECK_LIB(intl,main,[TOOLKIT_LIBS="$TOOLKIT_LIBS -lintl"])

fi # !motif
fi # with_motif

fi # libxaw

# if Motif not found, fall back to Xaw
if test x$no_motif = xyes
then
   if test -z "$TOOLKIT_LIBS"
   then
      XRUS_CHECK_XAW(Xaw3d)
      if test -z "$TOOLKIT_LIBS"; then
         XRUS_CHECK_XAW(Xaw)
      fi
   fi
   if test x$ac_cv_lib_Xaw_main != xyes -a x$ac_cv_lib_Xaw3d_main != xyes \
      || test x$with_Xaw = xno
   then
      AC_MSG_WARN(Neither Xaw3d, Xaw nor Motif found. Nothing will appear on the screen.)
   fi
fi

AC_SUBST(TOOLKIT_LIBS)

if uname -X 2>/dev/null | grep 3.2v5 >/dev/null
then
   # this dirty trick is used to force dynamic
   # compilation with gcc on sco v5
   LIBS="$LIBS /lib/libc.so"
fi

AC_OUTPUT(Makefile icons/Makefile keymaps/Makefile xmodmap/Makefile)
